; Este es el fichero de inicio de la librería para DAAD Reborn Compiler en espaÃ±ol


/CTL    ;SecciÂ¢n de control
#extern "MLV_ESX.BIN"
;#classic
#define TIMEOUT   87            ;If Timeout last frame

; System flags 0-63
#define fDark      0
#define fDarkF    28
#define fGFlags   29     ; This is best tested using HASAT GMODE
#define fTurns    31     ; 2 bytes
#define fTurnsHi  32

#define fVerb     33
#define fMaxCarr  37
#define fPlayer   38
#define fCPNoun   46
#define fCPAdject 47
#define fTime     48
#define fTIFlags  49
#define fDAObjNo  50
#define fCONum    51
#define fStrength 52
#define fOFlags   53
#define fCOLoc    54
#define fCOWei    55
#define fCOCon    56
#define fCOWR     57
#define fCOAtt    58
#define fKey1     60
#define fKey2     61
#define fScMode   62     ; 2=Text, 4=CGA, 13=EGA, 141=VGA
#define fCurWin   63     ; Which window is active at the moment
#define kk fCurWIn
;
;
;------------------------------------------------------------------------------
/VOC    ;Vocabulario
;                       Movimiento: verbos y nombres < 14
N       2       noun
Nó      2  noun
NORTE   2       noun
S       3       noun
SUR     3       noun
E       4       noun
ESTE    4       noun
O       5       noun
OESTE   5       noun
NE      6       noun
NO      7       noun
SE      8       noun
SO      9       noun
AR      10      noun
ARRIBA  10      noun
SUBIR   10      verb
AB      11      noun
ABAJO   11      noun
BAJAR   11      verb
;                       Nombres <20 indica que se pueden usar como verbos
;                               <50 indica nombre propio (no admite lo, la...)
I       14      noun
INVEN   14      noun
TODO    20      noun
TODOS   20      noun
TODAS   20      noun
;
LINTER  100     noun
CAZO    101     noun
;                       Verbs
COGER   20      verb
DEJAR   21      verb
SOLTAR  21      verb
QUITAR  22      verb
PONER   23      verb
R       24      verb
REDES   24      verb
F       25      verb
FIN     25      verb
SAVE    26      verb
SV      26      verb
LOAD    27      verb
LD      27      Verb
RAMSA   28      verb
RAMLO   29      verb
MIRAR   30      verb
EX      30      verb
EXAMI   30      verb
DECIR   31      verb
PREGUNT 31      verb
HABLAR  31      verb
JUGAR   32      verb
;
PEQUE   2       adjective
GRANDE  3       adjective
VIEJO   4       adjective
VIEJA   4       adjective
NUEVO   5       adjective
NUEVA   5       adjective
DURO    6       adjective
DURA    6       adjective
BLANDO  7       adjective
CORTO   8       adjective
CORTA   8       adjective
LARGO   9       adjective
LARGA   9       adjective
;                               Adverbios
RAPID   2       adverb
DESPA   3       adverb
LENTA   3       adverb
CUIDAd  5       adverb
;                               Prepositions
A       2       preposition
DE      3       preposition
EN      4       preposition
POR     5       preposition
TRAVES  5       preposition
BAJO    7       preposition
JUNTO   8       preposition
MENOS   13      preposition
;                               Pronouns
;                               Conjugations
Y       2       conjugation
ENTONCE 2       conjugation
LUEGO   2       conjugation
;
;------------------------------------------------------------------------------
/STX    ;Mensajes del sistema
/0 "Esta demasiado oscuro para ver nada."
/1 "También puedo ver:"
/2 "#n"
/3 "#n"
/4 "#n"
/5 "#n"
/6 "No he entendido nada."
/7 "No puedo ir en esa dirección."
/8 "No puedo hacer eso."
/9 "Llevo:#n"
/10 "Llevo puesto:"
/11 "";*Libre
/12 "¿Seguro?"
/13 "¿Quieres intentarlo de nuevo?"
/14 ""  ;*Libre
/15 "Vale."
/16 "Pulsa una tecla para continuar."
/17 "" ;*Has jugado
/18 "" ;*\sturno
/19 "" ;*s
/20 "" ; *.
/21 "" ; *Ha resuelto un
/22 "" ; *%
/23 "No llevo eso puesto."
/24 "No puedo. Tengo puesto _."
/25 "Ya tengo _."
/26 "Aquí no hay nada de eso."
/27 "No puedo llevar nada más."
/28 "No tengo eso.#n"
/29 "Ya llevo puesto _.#n"
/30 "S";Un solo carácter en mayúsculas. Inicial de Sí
/31 "N"  ;Un solo carácter en mayúsculas. Inicial de No
/32 "Hay más..."
/33 ">"
/34 "" ;*Libre
/35 "#nEl tiempo pasa..."
/36 "He cogido _."
/37 "Me he puesto _."
/38 "Me he quitado _."
/39 "He dejado _."
/40 "No puedo ponerme _."
/41 "No puedo quitarme _."
/42 "No puedo quitarme _.  No puedo llevar nada más en las manos."
/43 "@ pesa demasiado para mí."
/44 "@ está en"
/45 "@ no está en"
/46 ", "
/47 " y "
/48 ".#n"
/49 "No tengo _."
/50 "No llevo puesto _."
/51 ".#n"
/52 "No hay nada de eso en"
/53 "Nada."
/54 "C" ; Inicial de Cinta
/55 "D" ; Disco
/56 "Unidad no preparada. Pulsa una tecla para volver a intentarlo."
/57 "Error de entrada/salida."
/58 "El disco o el directorio puede estar lleno."
/59 "Nombre de fichero no válido."
/60 "Nombre del fichero:"
/61 "Pon en marcha la cinta."
/62 "¿Cinta o disco?"
;------------------------------------------------------------------------------
/MTX    ;Mensajes
/0 "PRUEBA: ñÑáéíóúüçÇ¡¿"
/1 "BEGIN En un lugar de la Mancha, de cuyo nombre no quiero acordarme, no ha mucho tiempo que vivía un hidalgo de los de lanza en astillero, adarga antigua, rocín flaco y galgo corredor. Una olla de algo más vaca que carnero, salpicón las más noches, duelos y quebrantos los sábados, lentejas los viernes, algún palomino de añadidura los domingos, consumían las tres partes de su hacienda. El resto della concluían sayo de velarte, calzas de velludo para las fiestas con sus pantuflos de lo mismo, los días de entre semana se honraba con su vellori de lo más fino. Tenía en su casa una ama que pasaba de los cuarenta, y una sobrina que no llegaba a los veinte, y un mozo de campo y plaza, que así ensillaba el rocín como tomaba la podadera. Frisaba la edad de nuestro hidalgo con los cincuenta años, era de complexión recia, seco de carnes, enjuto de rostro; gran madrugador y amigo de la caza. Quieren decir que tenía el sobrenombre de Quijada o Quesada (que en esto hay alguna diferencia en los autores que deste caso escriben), aunque por conjeturas verosímiles se deja entender que se llama Quijana; pero esto importa poco a nuestro cuento; basta que en la narración dél no se salga un punto de la verdad. Es, pues, de saber, que este sobredicho hidalgo, los ratos que estaba ocioso (que eran los más del año) se daba a leer libros de caballerías con tanta afición y gusto, que olvidó casi de todo punto el ejercicio de la caza, y aun la administración de su hacienda; y llegó a tanto su curiosidad y desatino en esto, que vendió muchas hanegas de tierra de sembradura, para comprar libros de caballerías en que leer; y así llevó a su casa todos cuantos pudo haber dellos; y de todos ningunos le parecían tan bien como los que compuso el famoso Feliciano de Silva: porque la claridad de su prosa, y aquellas intrincadas razones suyas, le parecían de perlas; y más cuando llegaba a leer aquellos requiebros y cartas de desafío, donde en muchas partes hallaba escrito: la razón de la sinrazón que a mi razón se hace, de tal manera mi razón enflaquece, que con razón me quejo de la vuestra fermosura, y también cuando leía: los altos cielos que de vuestra divinidad divinamente con las estrellas se fortifican, y os hacen merecedora del merecimiento que merece la vuestra grandeza. END"
/2 "Otro día"
/3 "mensaje de otros"
/4 "Hello world"
/5 "Hello"
;       -       -       -       -       -       -       -       -       -
/OTX    ;Object Texts
/0 "Una linterna.(encendida)"
/1 "Un cazo"
;       -       -       -       -       -       -       -       -       -
/LTX    ;Location Texts
/0 "BASE DE DATOS VACIA - DAAD V2.\nGeneración de código por compilador cañero traido por la cigüeña."
/1 "Has entrado a la mazmorra, múltiples peligros te esperan, pero también grandes logros y enormes tesoros. "
/2 "Estoy en la localidad numero dos. Un camino lleva al sur."
;------------------------------------------------------------------------------
/CON    ;Conexiones
/0
/1
N 2
E 2
S 1
/2
S 1
E 1
;------------------------------------------------------------------------------
/OBJ    ;Objetos
;obj  starts  weight    c w  5 4 3 2 1 0 9 8 7 6 5 4 3 2 1 0    noun   adjective
;num    at
/0      CARRIED 1       _ _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    LINTER  _
/1      2       1       Y _  _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _    CAZO  _
;------------------------------------------------------------------------------
/PRO 0       ;Bucle principal de localidades


_       _       AT      0               ; Empezamos una partida
                PROCESS 6               ; El proceso de inicialización
                
                

_       _       WINDOW  0               ; Ventana de gráficos
                CLEAR   fDarkF           ; Asume que hay luz
                NOTZERO 0
                ABSENT  0
                SET     fDarkF           ; Oscuro

_       _       PICTURE @fPlayer        ; Si hay gráfico, cÃ¡rgalo
                DISPLAY @fDarkF         ; Muestralo si no oscuro; si oscuro
                                        ; hace un CLS
_       _       WINDOW  1
                NOTZERO fDarkF           ; Oscuro
                SYSMESS 0

_       _       ZERO    fDarkF
                DESC   @fPlayer        ; DESC No sale del bucle ahora

_       _       PROCESS 3

; Ahora usamos el proceso 1 como el bucle principal del juego. Un retorno desde
; aquí significa el fin del juego

_       _       PROCESS 1

;------------------------------------------------------------------------------
/PRO 1

_       _       PROCESS 4               ; Lo que en el PAW era el proceso 2

_       _       PARSE   0               ; Toma sentencia lógica del buffer
                PROCESS 2               ; Sentencia no válida o timeout
                REDO

_       _       EQ      fTurns   255     ; El valo máximo de un byte
                PLUS    fTurnsHi   1
                CLEAR   fTurns
                SKIP    1

_       _       PLUS    fTurns   1

_       _       PROCESS 5               ; La antigua tabla de respuestas
                ISDONE                  ; Ha hecho algo?
                REDO                    ; Sí­: pedimos otra orden del jugador

_       _       MOVE    fPlayer          ; No: intentamos mover al jugador
                RESTART                 ;Lo movimos: salta al principio de PRO 0

_       _       NEWTEXT
                LT      fVerb    14
                SYSMESS 7
                REDO

_       _       SYSMESS 8
                REDO


;------------------------------------------------------------------------------
/PRO 2
_       _       HASAT TIMEOUT
                SYSMESS 35
                DONE

_       _       SYSMESS 6               ; No te entiendo

;------------------------------------------------------------------------------
/PRO 3 ; Antiguo proceso 1 del PAW. (Nótese que tanto la tabla de respuestas como los
; antiguos procesos 1 y 2 del PAW ahora pueden estar en cualquer parte; o no
; existir en absoluto, ya que ya no hay nada automático al haberse implementado
; todo en el propio lenguaje del DAAD)

_       _       NEWLINE
                ZERO    fDarkF            ; No está oscuro
                LISTOBJ


;------------------------------------------------------------------------------
/PRO 4 ; Antiguo proceso 2 del PAW

/PRO 5 ; Interpretamos los comando como en la antigua tabla de respuestas


I       _       MESSAGE 0
                MESSAGE 1
                SYSMESS 9
                LISTAT  CARRIED
                SYSMESS 10
                LISTAT  WORN
                DONE




COGER   TODO    DOALL   HERE                
COGER   _       AUTOG
                DONE

DEJAR   TODO    DOALL   CARRIED
DEJAR   _       AUTOD
                DONE

QUITAR  TODO    DOALL   WORN
QUITAR  _       AUTOR
                DONE


PONER   TODO    DOALL   CARRIED
PONER   _       AUTOW
                DONE

R       _       RESTART

FIN     _       QUIT
                END

FIN     _       DONE                    

SV      _       SAVE 0                  
                RESTART

LD      _       LOAD 0
                RESTART

RAMSA   _       RAMSAVE
                RESTART

RAMLO   _      
                RAMLOAD 255                             ;Reload all flags
                RESTART

MIRAR   _       RESTART

/PRO 6 ; Inicialización

_       _       WINDOW  1               ; Inicializamos las ventanas
                WINAT   14       0
                WINSIZE 25      127     ; Tamaño máximo
                CLS
                DESC    0               ; Introducción
                ANYKEY
                CLS
                CLEAR   255             ; Pone a cero todas las banderas

_       _       NOTEQ   255     fGFlags  ; menos GFlags!!!
                CLEAR   @255

_       _       PLUS    255     1
                LT      255     255     ; Al final lo dejamos con valor 255
                SKIP    -2              ; para indicar que hemos inicializado

_       _       RESET                   ; Objetos a su loc. inicial / Flag 1
                LET     fStrength 10
                LET     fMaxCarr  4
                SET     fCPNoun
                SET     fCPAdject
                GOTO    1               ; Primera localidad del juego
/END